plugins {
    `java-library`
    `maven-publish`
    checkstyle
}

fun getHash(): String {
    return Runtime.getRuntime()
        .exec("git rev-parse --short HEAD")
        .inputStream
        .bufferedReader()
        .readLine()
        ?.trim() ?: "00000000"
}

val lwjglVersion = "3.4.1"
val lwjglNatives = listOf(
    "natives-freebsd",
    "natives-linux-arm32", "natives-linux-arm64", 
	"natives-linux-ppc64le", "natives-linux-riscv64",
	"natives-linux",
    "natives-macos", "natives-macos-arm64",
    "natives-windows-x86", "natives-windows", "natives-windows-arm64",
)

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation(platform("org.lwjgl:lwjgl-bom:$lwjglVersion"))

    implementation("org.lwjgl", "lwjgl")
    implementation("org.lwjgl", "lwjgl-assimp")
    implementation("org.lwjgl", "lwjgl-glfw")
    implementation("org.lwjgl", "lwjgl-openal")
    implementation("org.lwjgl", "lwjgl-opengl")
    implementation("org.lwjgl", "lwjgl-stb")

    for (native in lwjglNatives) {
        runtimeOnly("org.lwjgl", "lwjgl", classifier = native)
        runtimeOnly("org.lwjgl", "lwjgl-assimp", classifier = native)
        runtimeOnly("org.lwjgl", "lwjgl-glfw", classifier = native)
        runtimeOnly("org.lwjgl", "lwjgl-openal", classifier = native)
        runtimeOnly("org.lwjgl", "lwjgl-opengl", classifier = native)
        runtimeOnly("org.lwjgl", "lwjgl-stb", classifier = native)
    }

    // Your other existing dependencies
    api(libs.org.l33tlabs.twl.pngdecoder)
    implementation("io.netty:netty-all:4.1.94.Final")
//    implementation("it.unimi.dsi:fastutil-core:8.5.16")

    implementation("com.code-disaster.steamworks4j:steamworks4j:1.10.0")
    implementation("com.code-disaster.steamworks4j:steamworks4j-lwjgl3:1.10.0")
}

group = "com.aehmttw"
version = rootProject.file("src/main/resources/version.txt").readText().trim()
rootProject.file("src/main/resources/hash.txt").writeText(getHash())
description = "Tanks"
java.sourceCompatibility = JavaVersion.VERSION_1_8


/*
*
*
*   Tanks Building Below Here
*
*
*
* */

// Force UTF-8 everywhere
tasks.withType<JavaCompile>().configureEach {
    options.encoding = "UTF-8"
}

tasks.withType<Test>().configureEach {
    systemProperty("file.encoding", "UTF-8")
}

tasks.withType<JavaExec>().configureEach {
    jvmArgs("-Dfile.encoding=UTF-8")
}

tasks.jar {
    archiveBaseName.set("Tanks")
    archiveVersion.set("${version}-${getHash()}")
    archiveClassifier.set("")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        from("src/main/java/META-INF/MANIFEST.MF")
    }

    from(configurations.runtimeClasspath.get().map { if (it.isDirectory) it else zipTree(it) })
}

task("run", type = JavaExec::class) {
    description = "Runs the JAR file"
    group = "application"
    dependsOn(tasks.jar)
    classpath(tasks.jar.get().outputs.files)
    mainClass.set("main.Tanks")
    workingDir = project.projectDir
    args("debug")
}

task("BuildMacApp", Exec::class) {
    dependsOn(tasks.jar)

    val distributions = file("build/distributions")
    val jpackage = org.gradle.internal.jvm.Jvm.current().javaHome.resolve("bin/jpackage")
    val libsDir = file("${layout.buildDirectory.get()}").resolve("libs")
    val jarName = tasks.named<Jar>("jar").get().archiveFileName.get()
    val baseName = tasks.named<Jar>("jar").get().archiveBaseName.get()
    val resourcesDir = sourceSets.main.get().resources.srcDirs.first()

    workingDir = projectDir

    delete(distributions.resolve("Tanks.app"))

    commandLine(
        jpackage,
        "--type", "app-image",
        "--input", libsDir,
        "--main-jar", jarName,
        "--name", baseName,
        "--resource-dir", resourcesDir,
        "--java-options", "-XstartOnFirstThread",
        "--arguments", "mac",
        "--dest", distributions,
    )
}

/*
*
*
*   Tanks Linting Below Here
*
*
*
* */

checkstyle {
    toolVersion = "9.3"
    configFile = file("config/checkstyle/checkstyle.xml")
    isIgnoreFailures = true
}

// Separate config so compileCustomChecks has the Checkstyle API on its classpath
// without creating a circular dependency back into the 'checkstyle' configuration.
val checkstyleTool by configurations.creating {
    isCanBeResolved = true
    isCanBeConsumed = false
}

val compileCustomChecks by tasks.registering(JavaCompile::class) {
    source = fileTree("config/checkstyle").matching { include("*.java") }
    classpath = configurations["checkstyleTool"]
    destinationDirectory.set(layout.buildDirectory.dir("custom-checkstyle-checks/classes"))
    sourceCompatibility = "8"
    targetCompatibility = "8"
    options.encoding = "UTF-8"
}

dependencies {
    checkstyleTool("com.puppycrawl.tools:checkstyle:${checkstyle.toolVersion}")
}

// Add the compiled custom-check classes to each Checkstyle task's tool classpath.
// We use checkstyleClasspath (the tool JARs) rather than classpath (the sources under analysis).
tasks.withType<org.gradle.api.plugins.quality.Checkstyle>().configureEach {
    dependsOn(compileCustomChecks)
    checkstyleClasspath = checkstyleClasspath.plus(
        files(compileCustomChecks.flatMap { it.destinationDirectory })
    )
}

tasks.register("lintCount") {
    description = "Runs Checkstyle and prints the violation count"
    group = "verification"
    dependsOn("checkstyleMain")
    doLast {
        val report = file("build/reports/checkstyle/main.xml")
        if (report.exists()) {
            val violations = javax.xml.parsers.DocumentBuilderFactory.newInstance()
                .newDocumentBuilder().parse(report)
                .getElementsByTagName("error").length
            val files = javax.xml.parsers.DocumentBuilderFactory.newInstance()
                .newDocumentBuilder().parse(report)
                .getElementsByTagName("file").length
            println("Checkstyle: $violations violations in $files files")
            println("Report: file://${file("build/reports/checkstyle/main.html").absolutePath}")
        } else {
            println("Checkstyle: no report found")
        }
    }
}
